// 1. Escribir DibujarBanda_EnDiagonalNEDe_x_(color, alto, largo) que
// dibuja una banda diagonal ancha.

procedure DibujarBanda_EnDiagonalNEDe_x_(color, alto, largo) {
    /*
        PROPÓSITO:
            * Dibuja una banda de color **color** en diagonal Noreste de **alto** de alto
            por **largo** de largo.
        PARÁMETROS:
            * color: Color - Es el color de las bolitas de la diagonal.
            * alto: Número - Es un número que describe el ancho de la banda.
            * largo: Número - Es un número que describe el largo de la diagonal.
        PRECONDICIONES:
            * Debe haber al menos **largo** celdas lindantes a la actual en la
              dirección Este, y (**alto** + **largo**) - 1 celdas en la dirección Norte.
    */
        repeat(largo-1){
            DibujarLínea_De_Alto(color, alto)
            SubirEnLaDiagonal()
        }
        DibujarLínea_De_Alto(color, alto)
        IrAlSudoeste_Veces(largo-1)
}

procedure DibujarLínea_De_Alto(color, alto) {
    /*
        PROPÓSITO:
            * Dibuja una línea de color **color** de **alto** celdas de alto.
              Deja el cabezal en la celda inicial.
        PARÁMETROS:
            * color: Color - Describe el color de la línea.
            * alto: Número - Un número que describe el alto de la línea.
        PRECONDICIONES:
            * Debe haber desde la celda actual, al menos **alto** - 1 celdas hacia el Norte.
    */
    repeat(alto-1){
        Poner(color)
        Mover(Norte)
    }
    Poner(color)
    
    repeat(alto-1){
        Mover(Sur)
    }
}

procedure SubirEnLaDiagonalNorEste() {
    /*
        PROPÓSITO:
            * Mueve el cabezal a la celda lindante a la actual en la dirección NorEste.
        PRECONDICIONES:
            * Debe haber al menos una celda lindante al Norte y otra al Este.
    */
    Mover(Este)
    Mover(Norte)
}

procedure IrAlSudoeste_Veces(numero) {
    /*
        PROPÓSITO:
            * Mueve el cabezal a la celda de inicio de la diagonal.
        PARÁMETROS:
            * largo: Número - Un número que describe el largo de la diagonal. 
        PRECONDICIONES:
            * Desde la celda actual debe haber al menos **largo** celdas lindantes al Sur, y
              **numeero** celdas lindantes al Oeste.
    */
    Mover_Al_(numero,Sur)
    Mover_Al_(numero,Oeste)
}

procedure Mover_Al_(cantidad, dirección) {
    /*
        PROPÓSITO:
            * Mueve el cabezal **cantidad** veces en la dirección **dirección**.
        PARÁMETROS:
            * cantidad: Es un Número - Un número que describe la cantidad de celdas que debe moverse el cabezal.
            * dirección: Es una Dirección - Una dirección que describe hacia dónde se mueve el cabezal.
        PRECONDICIONES:
            * Debe haber al menos **cantidad** celdas lindantes a la celda actual, en la dirección **dirección**.
    */
    repeat (cantidad) {
        Mover(dirección)
    }
}