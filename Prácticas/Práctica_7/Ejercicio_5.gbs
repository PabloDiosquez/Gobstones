// Escribir la funci贸n nroBolitas_EnLaFilaActual(color) que describa la
// cantidad de bolitas del color dado en la fila actual.
// a. Escribir la soluci贸n con un recorrido de la fila actual que utilice una variable
// cantidadDeBolitasYaVistas cuyo prop贸sito sea describir la cantidad
// de bolitas del color correspondiente que se contaron en cada momento.
// 驴Cu谩ntas se vieron antes de empezar a contar?  Antes de empezar a contar se vieron 0 bolitas de color **color**.
// 驴C贸mo estar seguro que se
// consideraron todas las celdas para contarlas?  Uso un recorrido que considere el caso borde.
// b. 驴En qu茅 celda queda el cabezal al utilizar la funci贸n desde cualquier punto
// del programa? 驴Por qu茅?  Queda en la misma celda desde donde se invoco a la funci贸n. 
// Una funci贸n con procesamiento no cambia el estado del tablero; solo importa el valor final calculado.

function nroBolitas_EnLaFilaActual(color) {
    /*
        PROPSITO:
            * Describe la cantidad de bolitas de color **color** en la fila actual.
        PARMETROS:
            * color: Color - Describe el color de las bolitas a contar en la fila actual.
        TIPO: N煤mero.
        PRECONDICIONES:
            * No tiene (es una funci贸n total).
        OBSERVACIONES:
            * Recorrido de la fila actual de Oeste a Este.
    */
    cantidadDeBolitasYaVistas := 0
    IrAlBorde(Oeste)
    while (puedeMover(Este)) {
        cantidadDeBolitasYaVistas := cantidadDeBolitasYaVistas + nroBolitas(color)
        Mover(Este)
    }
    cantidadDeBolitasYaVistas := cantidadDeBolitasYaVistas + nroBolitas(color)
    return (cantidadDeBolitasYaVistas)
}