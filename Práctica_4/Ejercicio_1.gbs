// 1. Escribir DibujarBanda_EnDiagonalNEDe_x_(color, alto, largo) que
// dibuja una banda diagonal ancha.

procedure DibujarBanda_EnDiagonalNEDe_x_(color, alto, largo) {
    /*
        PROPÓSITO:
            * Dibuja una banda de color **color** en diagonal Noreste de **alto** de alto
            por **largo** de largo.
        PARÁMETROS:
            * color: Color - Es el color de la diagonal.
            * alto: Número - Es un número que describe el alto de la banda.
            * largo: Número - Es un número que describe el largo de la diagonal.
        PRECONDICIONES:
            * Debe haber al menos **largo** celdas lindantes a la actual en la
              dirección Este, y (**alto** + **largo**) - 1 celdas lindantes en la dirección Norte.
    */
        repeat(largo-1){
            DibujarLínea_De_Alto(color, alto)
            IrALaDirección__VecesYALaDirección__Veces(Este,1,Norte,1)
        }
        DibujarLínea_De_Alto(color, alto)
        IrALaDirección__VecesYALaDirección__Veces(Sur,largo-1,Este,largo-1)
}

procedure DibujarLínea_De_Alto(color, alto) {
    /*
        PROPÓSITO:
            * Dibuja una línea de color **color** de **alto** celdas de alto.
              Deja el cabezal en la celda inicial.
        PARÁMETROS:
            * color: Color - Describe el color de la línea.
            * alto: Número - Un número que describe el alto de la línea.
        PRECONDICIONES:
            * Debe haber desde la celda actual, al menos **alto** - 1 celdas hacia el Norte.
    */
    repeat(alto-1){
        Poner(color)
        Mover(Norte)
    }
    Poner(color)
    Mover_Al_(alto-1, Sur)
}

procedure IrALaDirección__VecesYALaDirección__Veces(primerDirección, primerCantidad,segundaDirección, segundaCantidad) {
    /*
        PROPÓSITO:
            * Mueve el cabezal hacia la celda que está a **primerCantidad** celdas en la dirección **primerDirección** y a **segundaCantidad** celdas en la dirección **segundaDirección**.
        PARÁMETROS:
            * primerDirección: Dirección 
            * primerCantidad: Número 
            * segundaDirección: Dirección 
            * segundaCantidad: Número 
        PRECONDICIONES:
            * Desde la celda actual debe haber al menos **primerCantidad** de celdas lindantes en la dirección **primerDirección** y **segundaCantidad** de celdas lindantes en la dirección **segundaDirección**.
    */
    Mover_Al_(primerCantidad, primerDirección)
    Mover_Al_(segundaCantidad, segundaDirección)
}

procedure Mover_Al_(cantidad, dirección) {
    /*
        PROPÓSITO:
            * Mueve el cabezal **cantidad** veces en la dirección **dirección**.
        PARÁMETROS:
            * cantidad: Es un Número - Un número que describe la cantidad de celdas que debe moverse el cabezal.
            * dirección: Es una Dirección - Una dirección que describe hacia dónde se mueve el cabezal.
        PRECONDICIONES:
            * Debe haber al menos **cantidad** celdas lindantes a la celda actual, en la dirección **dirección**.
    */
    repeat (cantidad) {
        Mover(dirección)
    }
}